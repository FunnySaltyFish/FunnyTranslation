plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id "com.google.devtools.ksp" version "1.6.10-1.0.2"
}

android {
    signingConfigs {
        release {
            // 如果需要打release包，请在项目根目录下自行添加此文件
            /**
             *  STORE_FILE=yourAppStroe.keystore
                STORE_PASSWORD=yourStorePwd
                KEY_ALIAS=yourKeyAlias
                KEY_PASSWORD=yourAliasPwd
             */
            def props = new Properties()
            def propFile = new File("signing.properties")
            BufferedReader reader = new BufferedReader(new InputStreamReader(
                    new FileInputStream(propFile), "utf-8"));
            props.load(reader)

            storeFile file(props['STORE_FILE'])
            storePassword props['STORE_PASSWORD']
            keyAlias props['KEY_ALIAS']
            keyPassword props['KEY_PASSWORD']
        }
    }
    compileSdkVersion Versions.compileSdkVersion
    buildToolsVersion Versions.buildToolsVersion
    configurations.all {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            def requested = details.requested
            if (requested.group == 'com.android.support') {
                if (!requested.name.startsWith("multidex")) {
                    details.useVersion '28.0.0'
                }
            }
        }
    }
    defaultConfig {
        applicationId "com.funny.translation"
        minSdkVersion Versions.minSdkVersion
        targetSdkVersion 31
        versionCode 34
        versionName "2.2.4"
        resConfigs('zh-rCN','en')
        multiDexEnabled true
        ndk.abiFilters 'armeabi-v7a','arm64-v8a','x86','x86_64'

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation":"$projectDir/schemas".toString(),
                        "room.incremental":"true",
                        "room.expandProjection":"true"]
            }
        }
    }


    buildTypes {
        release {
            //临时可调式
           // debuggable true
            //开启代码混淆
            minifyEnabled true
            //Zipalign优化
            zipAlignEnabled true
            //移除无用的resource文件
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
            signingConfig signingConfigs.release
        }

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion Versions.composeVersion
    }
}

configurations.all {
    resolutionStrategy {
        force "androidx.appcompat:appcompat:1.4.1"
    }
}

dependencies {

    def compose_version = Versions.composeVersion
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.compose.animation:animation:$compose_version"
    implementation "androidx.navigation:navigation-compose:2.5.0-alpha01"
    implementation 'androidx.activity:activity-compose:1.5.0-alpha01'
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.5.0-alpha01"
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:${Versions.kotlinVersion}"

    def room_version = Versions.roomVersion
    implementation "androidx.room:room-runtime:$room_version"
    ksp "androidx.room:room-compiler:$room_version"

    implementation "com.github.FunnySaltyFish:CMaterialColors:1.0.21"
    implementation "com.airbnb.android:lottie-compose:4.2.0"
    implementation 'com.github.jeziellago:compose-markdown:0.2.6'

    def accompanist_version = "0.23.1"
    implementation "com.google.accompanist:accompanist-insets:$accompanist_version"
    implementation "com.google.accompanist:accompanist-systemuicontroller:$accompanist_version"
    implementation "com.google.accompanist:accompanist-flowlayout:$accompanist_version"
    implementation "com.google.accompanist:accompanist-navigation-animation:$accompanist_version"

    // 应用更新
//    implementation 'com.github.NewHuLe:AppUpdate:v1.7'
    implementation 'com.github.azhon:AppUpdate:3.0.6'

    // 刘海屏适配
    implementation 'com.github.smarxpan:NotchScreenTool:0.0.1'

    // 悬浮窗
//    implementation("com.github.PBK-B:FloatWindow:1.0.9")
    implementation project(path: ":float_window")
//    implementation "com.github.skydoves:powerspinner:1.1.9"
    implementation("androidx.appcompat:appcompat:1.4.1")

    implementation project(path: ":base-core")
    implementation project(path: ":codeeditor")
    implementation project(path: ":jet_setting_core")

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}