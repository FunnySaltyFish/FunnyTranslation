plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id "com.google.devtools.ksp" version "1.7.10-1.0.6"
}

android {
    signingConfigs {
        release {
            // 如果需要打release包，请在项目根目录下自行添加此文件
            /**
             *  STORE_FILE=yourAppStroe.keystore
                STORE_PASSWORD=yourStorePwd
                KEY_ALIAS=yourKeyAlias
                KEY_PASSWORD=yourAliasPwd
             */
            def props = new Properties()
            def propFile = new File("signing.properties")
            BufferedReader reader = new BufferedReader(new InputStreamReader(
                    new FileInputStream(propFile), "utf-8"))
            props.load(reader)

            storeFile file(props['STORE_FILE'])
            storePassword props['STORE_PASSWORD']
            keyAlias props['KEY_ALIAS']
            keyPassword props['KEY_PASSWORD']
        }
    }
    compileSdkVersion Versions.compileSdkVersion
    buildToolsVersion Versions.buildToolsVersion
    configurations.all {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            def requested = details.requested
            if (requested.group == 'com.android.support') {
                if (!requested.name.startsWith("multidex")) {
                    details.useVersion '28.0.0'
                }
            }
        }
    }
    defaultConfig {
        applicationId "com.funny.translation"
        minSdkVersion Versions.minSdkVersion
        targetSdk Versions.targetSdkVersion
        versionCode 44
        versionName "2.4.1"
        resConfigs('zh-rCN','en')
        multiDexEnabled true
        ndk.abiFilters 'armeabi-v7a','arm64-v8a','x86','x86_64'

        // KSP Room 配置
        ksp {
            arg('room.schemaLocation', "$projectDir/schemas")
            arg("room.incremental","true")
            arg("room.expandProjection","true")
        }
    }


    buildTypes {
        release {
            //临时可调式
           // debuggable true
            //开启代码混淆
            minifyEnabled true
            //Zipalign优化
            zipAlignEnabled true
            //移除无用的resource文件
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
            signingConfig signingConfigs.release
        }

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion Versions.composeVersion
    }
}

configurations.all {
    resolutionStrategy {
        force "androidx.appcompat:appcompat:${Versions.appCompatVersion}"
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-reflect:${Versions.kotlinVersion}"

    def room_version = Versions.roomVersion
    implementation "androidx.room:room-runtime:$room_version"
    ksp "androidx.room:room-compiler:$room_version"

//    implementation "androidx.work:work-runtime-ktx:2.7.1"

//    implementation 'com.github.FunnySaltyFish:FunnyBottomNavigation:v1.1.1'
    implementation "com.airbnb.android:lottie-compose:4.2.0"
//    implementation 'com.github.jeziellago:compose-markdown:0.2.6'

    // 分页加载
    implementation "androidx.paging:paging-compose:1.0.0-alpha15"
    implementation "androidx.paging:paging-runtime-ktx:3.1.1"

    // 应用更新
    implementation 'com.github.azhon:AppUpdate:3.0.6'

    implementation 'com.github.princekin-f:EasyFloat:2.0.4'
    implementation 'com.github.thomhurst:RoundImageView:1.0.2'
//    implementation "com.github.skydoves:powerspinner:1.1.9"
    implementation "androidx.appcompat:appcompat:${Versions.appCompatVersion}"
    implementation("org.burnoutcrew.composereorderable:reorderable:0.7.4")
    implementation "com.github.leavesCZY:Matisse:0.0.6"

    // 图片选择器
    implementation("cn.qhplus.emo:photo-coil:0.1.0")


    // Google Ad
//    implementation 'com.google.android.gms:play-services-ads:21.1.0'

    implementation project(path: ":base-core")
    implementation project(path: ":codeeditor")
    implementation project(path: ":login")
    implementation project(path: ":jet_setting_core")
    implementation project(path: ":bottom-navigation")

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}